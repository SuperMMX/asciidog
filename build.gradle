plugins {
    id 'application'
}

task publishToLocal

allprojects {
    group = 'org.supermmx.asciidog'
    version = '0.0.1-SNAPSHOT'

    repositories {
        mavenLocal()

        jcenter()
        mavenCentral()

        maven {
            url = 'https://github.com/psiegman/mvn-repo/raw/master/releases'
        }
    }
}

subprojects {
    /*
    plugins {
        id 'groovy'
        id 'maven-publish'
    }
    */

    apply plugin: 'groovy'
    apply plugin: 'maven-publish'


    //targetCompatibility = 1.6

    dependencies {
        implementation 'org.codehaus.groovy:groovy-all:3.0.8'
        implementation 'org.slf4j:slf4j-api:1.7.31'

        testImplementation('org.spockframework:spock-core:2.0-groovy-3.0') {
            exclude group:'org.codehaus.groovy'
        }

        runtimeOnly 'ch.qos.logback:logback-classic:1.2.3'

        testRuntimeOnly 'cglib:cglib-nodep:3.3.0'
    }

    compileGroovy {
        groovyOptions.configurationScript = file("${rootDir}/groovyConfig.groovy")
    }

    test {
        useJUnitPlatform()

        systemProperties System.properties
        testLogging.showStandardStreams = true
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
            }
        }
    }

    rootProject.publishToLocal.dependsOn tasks.matching { it.name == 'publishToMavenLocal' }
}

/*
task wrapper(type: Wrapper) {
    gradleVersion = '2.3'
}
 */

dependencies {
    runtimeOnly project(':asciidog-core')
    runtimeOnly project(':asciidog-html5')
    runtimeOnly project(':asciidog-epub')
    //runtime project(':asciidog-pdf')
    runtimeOnly project(':asciidog-critic')
}

mainClassName = 'org.supermmx.asciidog.AsciiDog'

run {
    // -Pargs="-i INPUT.adoc -o build"
    if (project.hasProperty('args')) {
        args = project.args.split(' ') as List
    }

    systemProperties System.properties
}
